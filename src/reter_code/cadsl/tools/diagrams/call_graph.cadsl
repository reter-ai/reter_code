# call_graph - Generate call graph from entry point
#
# Visualizes function/method call relationships as a Mermaid flowchart.
# Note: is-defined-in returns class name as string; calls links methods directly.

diagram call_graph() {
    """Generate call graph from entry point."""

    param target: str required;
    param max_depth: int = 10;
    param format: str = "mermaid";
    param skip: bool = false;

    reql {
        SELECT ?caller ?callee ?caller_name ?callee_name ?caller_class ?file ?line
        WHERE {
            ?caller calls ?callee .
            ?caller has-name ?caller_name .
            ?caller is-in-file ?file .
            ?caller is-at-line ?line .
            ?callee has-name ?callee_name .
            OPTIONAL {
                ?caller is-defined-in ?caller_class
            }
        }
        ORDER BY ?caller_name
    }
    | select { from_node: caller, to_node: callee, caller_name, callee_name, caller_class, file, line }
    | graph_traverse {
        from: from_node,
        to: to_node,
        algorithm: bfs,
        max_depth: {max_depth},
        root: {target}
    }
    | render_mermaid {
        type: flowchart,
        edges: from_node -> to_node,
        direction: TD
    }
    | view { skip: {skip}, description: "Call Graph" }
    | emit { diagram }
}
