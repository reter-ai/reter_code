# call_graph - Generate call graph from entry point
#
# Visualizes function/method call relationships as a Mermaid flowchart.
# Uses native graph_traverse and render_mermaid for pure declarative implementation.
#
# NOTE: Only static calls are included by default for performance.
# For include_maybe support, use call_graph_full.cadsl (much slower due to 44k+ maybe-calls).
#
# NOTE: Uses concept instead of type to support all languages (Python, C++, C#, JS)

diagram call_graph() {
    """Generate call graph from entry point (static calls only)."""

    param target: str required;
    param max_depth: int = 10;
    param format: str = "mermaid";

    reql {
        SELECT ?caller ?callee ?caller_name ?callee_name ?file ?line ?caller_type ?callee_type
        WHERE {
            ?caller calls ?callee .
            ?caller has-name ?caller_name .
            ?caller is-in-file ?file .
            ?caller is-at-line ?line .
            ?caller concept ?caller_type .
            ?callee has-name ?callee_name .
            ?callee concept ?callee_type .
            FILTER(?caller_type = "method" || ?caller_type = "function")
            FILTER(?callee_type = "method" || ?callee_type = "function")
            FILTER(!CONTAINS(?caller_name, "::"))
            FILTER(!CONTAINS(?callee_name, "::"))
        }
        ORDER BY ?caller_name
    }
    | select { from_node: caller, to_node: callee, caller_name, callee_name, file, line }
    | graph_traverse {
        from: from_node,
        to: to_node,
        algorithm: bfs,
        max_depth: {max_depth},
        root: {target}
    }
    | render_mermaid {
        type: flowchart,
        edges: from_node -> to_node,
        direction: TD
    }
    | emit { diagram }
}
